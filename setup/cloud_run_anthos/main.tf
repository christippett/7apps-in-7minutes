resource "null_resource" "cloud_run_anthos" {

  triggers = {
    name        = var.services.cloud_run_anthos.name
    description = var.services.cloud_run_anthos.description
    domain      = var.services.cloud_run_anthos.domain
    image       = "${var.image_name}:latest"
    opts = join(" ", [
      "--namespace default",
      "--platform gke",
      "--cluster ${var.cluster_name}",
      "--cluster-location ${var.zone}"
    ])
  }

  provisioner "local-exec" {
    when    = create
    command = <<EOT
    gcloud run deploy ${self.triggers.name} ${self.triggers.opts} \
      --set-env-vars 'APP_TITLE=${self.triggers.description}' \
      --image '${self.triggers.image}';
EOT
  }

  # https://cloud.google.com/run/docs/mapping-custom-domains
  # https://cloud.google.com/run/docs/gke/auto-tls

  provisioner "local-exec" {
    when    = create
    command = <<EOT
    gcloud beta run domain-mappings create ${self.triggers.opts} \
      --force-override \
      --service ${self.triggers.name} \
      --domain ${self.triggers.domain};

    kubectl patch cm config-domainmapping \
      --server https://${var.cluster_endpoint} \
      --token ${data.google_client_config.default.access_token} \
      --namespace knative-serving \
      --patch '{"data":{"autoTLS":"Enabled"}}';
EOT
  }

  provisioner "local-exec" {
    when    = destroy
    command = "gcloud run services delete ${self.triggers.name} ${self.triggers.opts}"
  }
}

/* DNS ---------------------------------------------------------------------- */

locals {
  access_token = data.google_client_config.default.access_token
  domain       = var.services.cloud_run_anthos.domain
  api_uri      = "https://${var.cluster_endpoint}/apis/domains.cloudrun.com/v1alpha1"
}

# get ip generated by domain mapping and convert it to a static ip

data "external" "domain_mapping" {
  program = ["bash", "-c", <<EOT
  curl --silent --show-error --fail \
    --insecure \
    --header "Authorization: Bearer ${local.access_token}" \
    --header "X-Goog-User-Project: ${var.project_id}" \
    "${local.api_uri}/namespaces/default/domainmappings/${local.domain}?alt=json" \
  | jq -r -c '.status.resourceRecords[0]' \
EOT
  ]
}

resource "google_compute_address" "static_ip" {
  project = var.project_id
  region  = var.region
  name    = "run-anthos-static-ip"
  address = data.external.domain_mapping.result.rrdata
}

resource "google_dns_record_set" "run_anthos" {
  project      = var.project_id
  name         = "${var.services.cloud_run_anthos.domain}."
  managed_zone = var.cloud_dns_zone
  type         = "A"
  rrdatas      = [google_compute_address.static_ip.address]
  ttl          = 300
}
